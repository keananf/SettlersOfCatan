/*
 * Declare Gradle plugins
 */
buildscript { repositories { jcenter() } }

plugins {
    id "java"
    id "com.google.protobuf" version "0.8.0"
    id "com.diffplug.gradle.spotless" version "3.0.0"
}

/*
 * Config. common to all projects
 */
allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    version = '1.0'
    ext {
        appName = "Settlers of Catan"
        gdxVersion = '1.9.5'
        aiVersion = '1.8.0'
        protobufVersion = "3.1.0"
        junitVersion = "4.12"
    }

    repositories { jcenter() }
}

/*
 * Config. specific to desktop project
 */
project(":desktop") {
    apply plugin: "java"

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    }
}

/*
 * Config. specific to core project
 */
project(":core") {
    apply plugin: "java"
    apply plugin: "com.google.protobuf"
    apply plugin: "com.diffplug.gradle.spotless"

    // turn on all warnings
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:all"
    }

    // declare dependencies
    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-ai:$aiVersion"
        compile "com.google.protobuf:protobuf-java:$protobufVersion"
        testCompile "junit:junit:$junitVersion"
    }

    // format source code
    spotless {
        java {
            eclipseFormatFile("../config/format.xml")
            removeUnusedImports()
            paddedCell()
        }
    }

    javadoc.destinationDir = file("${project.rootDir}/doc")

    protobuf {
        generatedFilesBaseDir = "$projectDir/gen"
        protoc {
            artifact = 'com.google.protobuf:protoc:3.2.0'
        }
    }

    clean {
        delete protobuf.generatedFilesBaseDir
    }

    idea {
        module {
            sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java");
            sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc");
        }
    }

    eclipse {
        classpath {
            file.beforeMerged {
                cp -> cp.entries.add(new org.gradle.plugins.ide.eclipse.model.SourceFolder("${protobuf.generatedFilesBaseDir}/main/java", null))
                      cp.entries.add(new org.gradle.plugins.ide.eclipse.model.SourceFolder("${protobuf.generatedFilesBaseDir}/main/grpc", null))
            }
        }
    }
}

tasks.eclipse.doLast {
    delete ".project"
}
